1000 '===    Logic Puzzle Solver
1010 '===
1020 '===     DATA-MAKER   Ver 1.00
1030 '===             '95/3/15-'95/5/6
1040 '
1050 '=== Init ===
1060 SCREEN0:WIDTH40:COLOR15,1,1:CLS
1070 DEFINTA-Z
1080 '=== Select Menu ===
1090 PRINT"1:New / 2:Load"
1100 K$=INPUT$(1)
1110 IF K$="2" THEN 1960
1120 IF K$<>"1" THEN 1100
1130 '=== New Data ===
1140 '--- Input ---
1150 INPUT"Height,Width";HT,WD
1160 IF HT<1 OR HT>255 OR WD<1 OR WD>255 THEN 1150
1170 IF HT>WD THEN BM=(HT+1)\2 ELSE BM=(WD+1)\2
1180 DIM BL(BM-1,HT+WD-1),NB(HT+WD-1)
1190 FOR N=0 TO HT+WD-1
1200 IF N<HT THEN MX=WD ELSE MX=HT
1210 GOSUB 2050
1220 NEXT
1230 '--- Small Menu for New-Data ---
1240 BEEP:CLS
1250 PRINT"1:Check / 2:Fix / 3:Data Make"
1260 K$=INPUT$(1)
1270 IF K$="3" THEN 1500
1280 IF K$="2" THEN 1400
1290 IF K$<>"1" THEN 1260
1300 '--- Review Data ---
1310 FOR N=0 TO HT+WD-1
1320 GOSUB 2180
1330 IF N MOD 10=9 THEN GOSUB 1370
1340 NEXT
1350 IF N MOD 10 THEN GOSUB 1370
1360 GOTO 1240
1370 PRINT"Push any key!!":BEEP:K$=INPUT$(1)
1380 RETURN
1390 '--- Fix Data ---
1400 DT$="":INPUT"Where";DT$
1410 IF DT$="" THEN 1240
1420 K$=LEFT$(DT$,1):N=VAL(MID$(DT$,2))-1
1430 IF (K$="R" OR K$="r")AND(N>=0 AND N<HT) THEN MX=WD:GOTO 1450
1440 IF (K$="C" OR K$="c")AND(N>=0 AND N<WD) THEN MX=HT:N=N+HT ELSE BEEP:GOTO 1400
1450 GOSUB 2180
1460 PRINT:PRINT"     V":PRINT"     V":PRINT
1470 GOSUB 2050
1480 GOTO 1400
1490 '--- VRAM Data Make ---
1500 SCREEN5,2:SETPAGE0,0:CLS
1510 OPEN"GRP:"AS#1
1520 PRESET(40,90):PRINT#1,"Now setting work area."
1530 SETPAGE,2
1540 A1!=32:A2!=A1!+(WD+2)*(HT+2):A3!=A2!+(HT+2)*(WD+2)
1550 VPOKE 0,HT:VPOKE 1,WD
1560 FOR AD!=A1! TO A3!-1:VPOKE AD!,254:NEXT
1570 FOR AD!=A1! TO A1!+WD:VPOKE AD!,255:NEXT
1580 FOR AD!=A1!+WD+2 TO A2!-WD-2 STEP WD+2:VPOKE AD!-1,255:VPOKE AD!,255:NEXT
1590 FOR AD!=A2!-WD-1 TO A2!+HT:VPOKE AD!,255:NEXT
1600 FOR AD!=A2!+HT+2 TO A3!-HT-2 STEP HT+2:VPOKE AD!-1,255:VPOKE AD!,255:NEXT
1610 FOR AD!=A3!-HT-1 TO A3!-1:VPOKE AD!,255:NEXT
1620 AD!=A3!+(HT+WD)*5:BM=0
1630 FOR N=0 TO HT+WD-1
1640 IF BM<NB(N) THEN BM=NB(N)
1650 VPOKE A3!+N*5,NB(N)
1660 VPOKE A3!+N*5+4,0
1670 IF NB(N)>0 THEN 1710
1680 VPOKE A3!+N*5+1,0
1690 VPOKE A3!+N*5+2,0:VPOKE A3!+N*5+3,0
1700 GOTO 1850
1710 SU=0
1720 FOR I=0 TO NB(N)-1
1730 SU=SU+BL(I,N)
1740 NEXT
1750 IF N<HT THEN MX=WD ELSE MX=HT
1760 F1=MX+2-(SU+NB(N))
1770 VPOKE A3!+N*5+1,F1
1780 VPOKE A3!+N*5+2,AD!MOD256:VPOKE A3!+N*5+3,AD!\256
1790 FOR I=0 TO NB(N)-1
1800 VPOKE AD!,BL(I,N)
1810 AA!=AD!:FOR AD!=AA!+1 TO AA!+F1
1820 VPOKE AD!,1
1830 NEXT
1840 NEXT
1850 NEXT
1860 A4!=AD!
1870 VPOKE 2,A1!MOD256:VPOKE 3,A1!\256
1880 VPOKE 4,A2!MOD256:VPOKE 5,A2!\256
1890 VPOKE 6,A3!MOD256:VPOKE 7,A3!\256
1900 VPOKE 8,A4!MOD256:VPOKE 9,A4!\256
1910 VPOKE10,BM:VPOKE11, 0
1920 VPOKE12, 0:VPOKE13, 1
1930 VPOKE29,1:VPOKE30,2:VPOKE31,3
1940 GOTO 2020
1950 '=== Load File ===
1960 FILES:PRINT:PRINT
1970 FL$="":INPUT"Load Filename";FL$
1980 IF FL$="" THEN 1970
1990 SCREEN5,2:SETPAGE0,2
2000 BLOAD FL$,S
2010 '=== Load Main-Program ===
2020 RUN"LPS1.BAS"
2030 END
2040 '=== Input Sub ===
2050 GOSUB 2240
2060 I=0:SU=0
2070 LINEINPUT DT$
2080 IF VAL(DT$)=0 THEN 2160
2090 LN=LEN(DT$):CM=INSTR(DT$,",")
2100 IF CM=0 THEN DT=VAL(DT$):DT$="":GOTO 2130
2110 DT=VAL(LEFT$(DT$,CM-1))
2120 DT$=MID$(DT$,CM+1)
2130 IF SU+DT+I>MX THEN PRINT"Over!":BEEP:GOTO 2050
2140 BL(I,N)=DT:SU=SU+DT:I=I+1
2150 GOTO 2080
2160 NB(N)=I:RETURN
2170 '=== Review Sub ===
2180 GOSUB 2240
2190 IF NB(N)=0 THEN PRINT 0:RETURN
2200 FOR I=0 TO NB(N)-1
2210 PRINT BL(I,N);
2220 NEXT:PRINT:RETURN
2230 '=== Row/Column Print Sub ===
2240 IF N>=HT THEN 2260
2250 PRINT"Row   ";N+1   ;"Data:";:RETURN
2260 PRINT"Column";N-HT+1;"Data:";:RETURN
